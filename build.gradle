plugins {
	id 'java'
	id 'groovy'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'maven'
	id 'com.jfrog.bintray' version '1.4'
}

group = 'com.github.aesteve'
version = '0.6'

sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	compile "io.vertx:vertx-core:$vertxVersion"
	compile "io.vertx:vertx-web:$vertxVersion"
	compile "io.vertx:vertx-lang-groovy:$vertxVersion"
	compile "io.vertx:vertx-unit:$vertxVersion"
	compile "io.vertx:vertx-auth-oauth2:$vertxVersion"

	testCompile 'junit:junit:4.12'
	testCompile 'org.apache.ant:ant:1.9.6'
	testCompile "io.vertx:vertx-web-templ-handlebars:$vertxVersion"
	testRuntime 'com.github.jknack:handlebars:1.3.0'
}

bintray {
	user = 'aesteve'
	key = System.getenv('BINTRAY_KEY')
	pkg {
		repo = 'maven'
		name = project.name
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/aesteve/vertx-groovy-sugar'
		labels = ['groovy', 'vertx', 'vert.x']
		publicDownloadNumbers = true
		version {
			name = project.version
			description = 'Extensions + Promises'
		}
		publications = ['maven']
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifact sourcesJar
		}
	}
}

task sourcesJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allGroovy
}

// Demos

sourceSets {
	demos {
		groovy {
			compileClasspath += test.output
			runtimeClasspath += test.output
		}
	}
}

configurations {
	demosCompile.extendsFrom compile
}

task demo(dependsOn: 'classes', type: JavaExec) {
	String demoName = project['demo']
	main = "$demoName/launchDemo"
	classpath = sourceSets.demos.runtimeClasspath
}


task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}
